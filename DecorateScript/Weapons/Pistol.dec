ACTOR EquestriaPistol : DoomEquestriaWeapon replaces Pistol
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	
	Obituary "$OB_MPPISTOL"
	
	+WEAPON.WIMPY_WEAPON
	
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	
	Tag "$TAG_PISTOL"
	
	States
	{

	Spawn:
		PIST A -1
		Stop
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "KickAttack")
		
		PISG A 1 A_WeaponReady
		Loop
		
	Select:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
		
		PISG A 0 A_SetCrosshair(8)
		PISG A 1 A_Raise
		Wait
	
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999)
		
		PISG A 1 A_Lower
		Wait
	
	Fire:
		PISG A 4
		PISG B 6 
		{
			A_WeaponOffset(0, 30, WOF_KEEPX | WOF_INTERPOLATE);
			A_FireBullets (5.6, 0, 1, 5, "BulletPuff");
			A_PlaySound("weapons/pistol", CHAN_WEAPON);
			A_GunFlash;
		}
		PISG C 4 A_WeaponOffset(0, 29, WOF_KEEPX | WOF_INTERPOLATE)
		PISG D 5 
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire;
		}
	Goto Ready
  
	Flash:
		PISF A 3 Bright
		Goto LightDone
	
	}
}
