Class DoomEquestriaWeapon : Weapon
{
	Default
	{
		Weapon.Kickback 100;
		Weapon.BobStyle "Smooth";
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeX 0.5; Weapon.BobRangeY 0.75;
		
		+ForceXYBillBoard;
	}
	States
	{
	GotoReady:
		TNT1 A 0 A_Jump(256, "Ready");
		Wait;
		
	DeathCheck:
		TNT1 A 1 Offset(0,32)
		{
			A_ClearOverlays();
			A_OverlayScale(PSP_WEAPON, 1, 1);
			A_StopSound(CHAN_BODY);
			A_StopSound(CHAN_WEAPON);
			A_StopSound(CHAN_6);
			A_ZoomFactor(1.0, ZOOM_INSTANT);
		}
		TNT1 A 0 A_Overlay(2, "CameraRoll");
	DeathNormal:
	DeathBlast:
		FDTH A 1 A_OverlayScale(-3, 1, 1, WOF_INTERPOLATE);
		FDTH A -1;
		Stop;
	
	CameraRoll:
		TNT1 A 0 A_Jump(256, 1, "CameraRoll2");
		Wait;
		TNT1 A 1
		{
			A_OverlayFlags(1, PSPF_PLAYERTRANSLATED, 1);
			A_SetPitch(frandom(-25, 25), SPF_INTERPOLATE);
			A_SetRoll(frandom(0, 50), SPF_INTERPOLATE);
		}
		TNT1 A 1 A_JumpIf(health >= 1, 1);
		Wait;
		TNT1 A 1
		{
			A_OverlayFlags(1, PSPF_PLAYERTRANSLATED | PSPF_FLIP, 0);
			A_SetPitch(0);
			A_SetRoll(0);
		}
		Stop;
	CameraRoll2:
		TNT1 A 1
		{
			A_OverlayFlags(1, PSPF_PLAYERTRANSLATED, 1);
			A_OverlayFlags(1, PSPF_FLIP, 1);
			A_SetPitch(frandom(-25, 25), SPF_INTERPOLATE);
			A_SetRoll(frandom(-50, 0), SPF_INTERPOLATE);
		}
		TNT1 A 1 A_JumpIf(health >= 1, 1);
		Wait;
		TNT1 A 1
		{
			A_OverlayFlags(1, PSPF_PLAYERTRANSLATED | PSPF_FLIP, 0);
			A_SetPitch(0);
			A_SetRoll(0);
		}
		Stop;
		
////Deaths

  /*ToDo	ToDo	ToDo
	Death.Common:
		
	
	Death.Lava:
	
	Death.Nuke:
	
	Death.Fire:
	
	ToDo	ToDo	ToDo*/

////Kicks

	Kick.Normal:
		TNT1 A 0 A_TakeInventory ("KickCheck", 999);
		
		TNT1 A 0 A_JumpIf(Vel.Z > 0, "Kick.Air");
		TNT1 A 0 A_JumpIf(Vel.Z < 0, "Kick.Air");
		
		TNT1 A 0
		{
			A_StartSound ("skeleton/swing");
			A_AlertMonsters (250, 0);
			A_Overlay(-3, "Kick.Overlay");
			A_Overlay(3, "Overlay.Aside");
		}
		
        TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Kick.Berserk");
				
		TNT1 A 0 SetPlayerProperty(0, 1, PROP_FROZEN);

		TNT1 AAA 1;
		TNT1 A 0 A_CustomPunch(2 * Random(1,10), true, 0, "KickPuff", 75, 0, 0, "BasicArmorBonus", "*fist");
		TNT1 A 6;
		TNT1 A 0 SetPlayerProperty(0, 0, 0);
		TNT1 AAAAA 2;
		
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		Goto GotoReady;
	
	Kick.Berserk:
		TNT1 AAA 1;
        TNT1 A 0 A_CustomPunch(20 * Random(1, 10), TRUE, 0, "BerserkPuff", 75, 0, 0, "BasicArmorBonus", "skeleton/melee");
        TNT1 A 6;
		TNT1 AAAAA 2;
		
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		Goto GotoReady;
		
	Kick.Air:
		TNT1 A 0
		{
			SetPlayerProperty(0, 1, PROP_FROZEN);
			A_StartSound ("skeleton/swing");
			A_AlertMonsters (500, 0);
			A_Overlay(-3, "Kick.Overlay");
			A_Overlay(3, "Overlay.AirAside");
		}

		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Kick.BerserkAir");
		
		TNT1 A 0 A_Recoil(-6);
		TNT1 AAAAA 1;
        TNT1 A 0 A_CustomPunch(2*Random(1,10), true, 0, "KickPuff", 75, 0, 0, "BasicArmorBonus", "*fist");
        TNT1 A 2 A_AlertMonsters (500, 0);
		TNT1 AAAAA 2;
		
		TNT1 A 0 SetPlayerProperty(0, 0, 0);
		
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		Goto GotoReady;
		
	Kick.BerserkAir:
		TNT1 A 0 A_Recoil(-6);
		TNT1 AAAAA 1;
        TNT1 A 0 A_CustomPunch(20 * Random(1, 10), TRUE, 0, "BerserkPuff", 75, 0, 0, "BasicArmorBonus", "skeleton/melee");
        TNT1 A 2 A_AlertMonsters (500, 0);
		TNT1 AAAAA 2;
		
		TNT1 A 0 SetPlayerProperty(0, 0, 0);
		
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		Goto GotoReady;
		
	Kick.Overlay:
		TNT1 A 0 A_OverlayFlags(-3, PSPF_PLAYERTRANSLATED, true);
		
		KICK BCD 1 A_OverlayScale(-3, 1, 1, WOF_INTERPOLATE);
		KICK H 6 A_OverlayScale(-3, 1, 1, WOF_INTERPOLATE);
		KICK FEDCB 2 A_OverlayScale(-3, 1, 1, WOF_INTERPOLATE);
		Stop;
		
	Overlay.Aside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHTG AEE 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 4, WOF_ADD);
		}
		SHTG FF 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 8, WOF_ADD);
		}
		SHTG F 10;
		SHTG FEE 1 
		{
			A_OverlayOffset(3, 0, 0, WOF_INTERPOLATE );
			A_OverlayRotate(3, -8, WOF_ADD);
		}
		SHTG A 1;
		Stop;
	
	Overlay.AirAside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHTG AEE 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 5, WOF_ADD);
		}
		SHTG FF 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 10, WOF_ADD);
		}
		SHTG F 8;
		SHTG FEE 1
		{
			A_OverlayOffset(3, 0, 0, WOF_KEEPY | WOF_INTERPOLATE );
			A_OverlayRotate(3, -10, WOF_ADD);
		}
		SHTG A 1;
		Stop;
		
	Off:
		TNT1 A 0;
		Stop;
	}
}

Class BulletTracer : FastProjectile
{
	Default
	{
		Projectile;
		
		+DONTSPLASH
		+NOEXTREMEDEATH
		+NOBLOOD
		+NOPAIN
		+Bright
		
		Speed 200; FastSpeed 400;
		Damage 0;
		RenderStyle "Add";
		Alpha 0.75;
		
		Height 2; Radius 1;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		PUFF A 1;
		Wait;
	
	Death:
		TNT1 A 1;
		Stop;
	}
}