Class EquestriaCyberdemon : Cyberdemon replaces Cyberdemon
{
	Default
	{
		//$Sprite CYBRA2
		+CASTSPRITESHADOW;
		//Health 5000;
		Mass 1500;
	}
	States
	{
	Spawn:
		IDLY AB 10 A_Look();
		Loop;
		
	 Missile:
		CYBR E 6 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 45, -26);
		CYBR E 12 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 45, -26);
		CYBR E 12 A_FaceTarget();
		CYBR F 12 A_SpawnProjectile("EquestriaRocket", 45, -26);
		Goto See;
   
	Death:
		CYBR H 10;
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("CyberdemonExplosion", frandom(-40, 40), frandom(-40, 40), frandom(32, 110));
		CYBR I 10
		{
			A_SpawnItemEx("XDeathGibber",0,0,random(32, 75),0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,random(32, 50),0,0,0,0, SXF_TRANSFERPOINTERS);
			A_Scream();
			A_NoBlocking();
		}
		CYBR J 8;
		CYBR KL 5
		{
			A_QuakeEx(4, 4, 4, 35, 0, 800, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
			
			A_SpawnItemEx("BigGibber",0,0,random(32, 110),0,0,0,0, SXF_TRANSFERPOINTERS);
		}
		TNT1 A 0
		{
			for (int i = 0; i < 25; i++)
			{
				A_SpawnProjectile("EquestriaBlood", frandom(32, 80), frandom(-80, 80), random (0, 359), CMF_AIMDIRECTION, random (0, -180));
				A_SpawnProjectile("BloodTrail", frandom(32, 80), frandom(-80, 80), random (0, 359), CMF_AIMDIRECTION, random (0, -180));	
				A_SpawnProjectile("SmallBloodTrail", frandom(32, 80), frandom(-80, 80), random (0, 359), CMF_AIMDIRECTION, random (0, -180));	
			}
		}
			
		CYBR M 5 A_NoBlocking;
		CYBR NO 5;
		CYBR P 30;
		CYBR P -1 A_BossDeath;
		Stop;
	}
}

Class CyberdemonExplosion : DEParticles
{
	Default
	{
		Speed 0;
		RenderStyle "Translucent"; Alpha 0.75;
		+NOINTERACTION
		+ZDOOMTRANS
	}
	States
	{
	Spawn:
		TNT1 A random(0,16) NoDelay
		{
			A_SetScale(frandom(0.5, 1.0));
			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile("EquestriaBlood", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
				A_SpawnProjectile("BloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));	
				A_SpawnProjectile("SmallBloodTrail", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));	
			}
		}
		EXP2 A random(3,6);
		EXP2 B 4;
		EXP2 C 1 A_FadeOut(0.1);
		Wait;
	}
}