Class EquestriaArachnotron : Arachnotron replaces Arachnotron
{
	Default
	{
		+CastSpriteShadow
		Translation "160:167=112:127";
	}
	States
	{
	
	Missile:
		BSPI A 20 A_FaceTarget();
		BSPI G 4 bright A_SpawnProjectile("EquestriaAracPlasmaBall", 18, 4);
		BSPI H 4 bright;
		BSPI H 1 bright A_SpidRefire();
		Goto Missile+1;
   
	}
}

Class EquestriaAracPlasmaBall : EquestriaPlasmaBall replaces ArachnotronPlasma
{
	Default
	{
		SeeSound "baby/attack";
		DeathSound "baby/shotx";
	}
	States
	{
	Spawn:
		APLS A 0 NoDelay A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		APLS A 0 A_SetRoll(random(0, 359));
		APLS A 1 Bright
		{
			A_SpawnItem("AracPlasmaTrail");
			A_SetRoll((roll + 15), SPF_INTERPOLATE);
		}
		Wait;
 
	Death:
		APLS A 0 A_SpawnItem("SmokePuff");
		APLS AAAA 0 A_SpawnProjectile("AracPlasmaSparkle", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		APLS ABC 4 Bright;
		APLS DE 4 A_FadeOut(0.1);
		Stop;
	}
}

Class AracPlasmaTrail : PlasmaTrail
{
	States
	{
	Spawn:
		APLS A 0;
		APLS A 0 A_SetRoll(random(0, 359));
	Trail:
		APLS A 1 Bright
		{
			A_SetRoll((roll + 15), SPF_INTERPOLATE);
			A_FadeOut(0.1);
		}
		Loop;
	}
}

Class AracPlasmaSparkle : PlasmaSparkle
{
	States
	{
	Spawn:
		APLS A 0 NoDelay A_SetRoll(random(0, 359));
		
		APLS A random(15,25);
		APLS AAAA 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		APLS AAAA 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
}