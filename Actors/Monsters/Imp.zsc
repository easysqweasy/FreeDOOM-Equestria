Class EquestriaImp : DoomImp Replaces DoomImp
{
	Default
	{
		+CASTSPRITESHADOW;
		
		+ROLLSPRITE; +ROLLCENTER;
	}
	States
	{
	Spawn:
		TROO AB 10 A_Look();
		Loop;
	
	See:
		TROO AABBCCDD 3 A_Chase();
		Loop;
		
	/*Idle:
		TROO AABBCCDD 3
		{
			A_Wander();
			A_Look();
		}
		TROO A 0 A_Jump(32, "Tired");
		Loop;
	
	Tired:
		IDLI AB 10 A_Look();
		TROO A 0 A_Jump(128, "Idle");
		Loop;*/
	
	Melee:
		TROO E 8 A_FaceTarget();
		TROO F 6 A_FaceTarget();
		TROO G 0 A_Recoil(-4);
		TROO G 6 A_CustomMeleeAttack(3 * random(1, 8), "imp/melee");
		TROO G 0 A_Stop;
		Goto See;
		
	Missile:
		TROO EF 8 A_FaceTarget();
		TROO G 6 A_SpawnProjectile("DoomImpBall");
		Goto See;
	
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
		TROO H 2;
		TROO H 2 A_Pain;
		Goto See;
	
	Death.Plasma:
		#### # 0 A_SetTranslation("Toasted");
		#### ##### 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		TROO I 5;
		TROO J 5 A_Scream();
		#### K 4 A_NoBlocking();
        #### K 1 A_CheckFloor(1);
		Wait;
		#### L 5;
		#### M -1;
		Stop;
		
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(64, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		Goto Death;
		
	XDeath:
		#### # 0 { bSPRITEFLIP = randompick(0, 1); }
		#### # 0
		{
			A_SpawnItemEx("XDeathGibber",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		TROO NOPQRST 4;
		TROO U -1;
		Stop;
		
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		TROO J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
		TROO ML 8;
		TROO KJI 6;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
		
	}
}

Class EquestriaImpBall : DoomImpBall replaces DoomImpBall
{
	Default
	{
		Radius 8; Height 16;
	
		DamageType "Fire";
		
		Decal "FireBallScorch";
		
		+ForceXYBillBoard
		+Bright
	}
	States
	{
	Spawn:
		BAL1 B 2 A_SpawnItem("SmokePuff");
		BAL1 A 2 A_SpawnItem("FireBallTrail");
		Loop;
		
	Death:
		#### # 0 A_SpawnItem("SmokePuff");
		BAL1 CD 6;
		BAL1 E 1 A_FadeOut(0.1);
		Wait;
	}
}

Class FireBallTrail : Trails
{
	Default { +ZDOOMTRANS }
	States
	{
	Spawn:
		BAL1 CD 3 Bright A_FadeOut(0.125);
		BAL1 E 1 Bright A_FadeOut(0.1);
		Wait;
	}
}