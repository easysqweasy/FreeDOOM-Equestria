Class EquestriaShotgunGuy : DoomEquestriaMonster replaces ShotgunGuy
{
	Default
	{
		Monster;
		
		Health 30;
		Radius 20; Height 56;
		Mass 100;
		Speed 8;
		
		PainChance 170; PainThreshold 5;
		
		+CASTSPRITESHADOW;
		+FLOORCLIP;
		
		SeeSound "shotguy/sight";
		AttackSound "shotguy/attack";
		PainSound "shotguy/pain";
		DeathSound "shotguy/death";
		ActiveSound "shotguy/active";
		
		Obituary "$OB_SHOTGUY";
		
		Tag "$FN_SHOTGUN";
		
		DropItem "EquestriaShotgun";
	}
	States
	{
	
	Spawn:
		SPOS A 10 A_Look();
		Loop;
	
	See:
		SPOS AABBCCDD 3 A_Chase();
		Loop;
	See.Trainer:
		#### AABBCCDD 3 Fast
		{
			A_Chase("Melee", "Missile", CHF_FASTCHASE);
			A_SpawnProjectile("DEWaterSplashChunk", random(32, 56), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		}
		Goto See;
		
	Missile:
		SPOS E 10 A_FaceTarget();
		SPOS F 10 Bright A_CustomBulletAttack(25, 20, 8, random(1,3)*3, "ShotgunPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
		SPOS E 10;
		Goto See;
	
	Pain:
        SPOS G 3;
        SPOS G 3 A_Pain();
        Goto See;
	
	Death.Plasma:
		"####" "#" 0 A_SetTranslation("Toasted");
		"####" "#####" 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
        SPOS H 5;
        SPOS I 5 A_Scream();
        #### J 4 A_NoBlocking();
        #### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
    
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		Goto Death;
		
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
        SPOS M 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		SPOS NOPQRST 5;
        SPOS U -1;
        Stop;
    
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		SPOS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
        SPOS LKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
        Goto See;
    }
}
