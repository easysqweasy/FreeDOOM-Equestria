Class PossessedHacker : Actor
{
	Default
	{
	Monster;
	//$Category Monsters
	//$Title Possessed Hacker
		Tag "$TECHDUDE";
		
		Health 15;
		Radius 20; Height 56;
		Mass 80;
		Speed 10; FastSpeed 15;
		Species "Couple";
		PainChance 170;
		
		+FloorClip
		+MissileMore
		+CastSpriteShadow
		
		SeeSound "female/sight";
		AttackSound "";
		PainSound "female/pain";
		DeathSound "female/death";
		ActiveSound "female/active";
		
		Obituary "$OB_PLASMAKIRIN";
		
		DropItem "ArmorBonus";
		DropItem "Cell", 64; //25% Chance of drop Cells
		
		MissileType "EquestriaPlasmaBall";
	}
	States
	{
	
	Spawn:
		TECH AB 10 A_Look();
		Loop;
	
	See:
		//#### # 0 A_CheckProximity("Raise", "PossessedTrainer", 1024, 1);
		TECH AABBCCDD 3 A_Chase();
		Loop;
	
	Missile:
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall");
		}
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall");
		}
		TECH E 1;
		TECH F 2 Bright 
		{
			A_FaceTarget();
			A_SpawnProjectile("EquestriaPlasmaBall");
		}
		TECH E 20 A_CposRefire();
		Goto See;
	
	Pain.Kick:
		#### # 0 A_Recoil(5);
	Pain:
		TECH G 3;
		TECH G 3 A_Pain();
		Goto See;
	
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		TECH H 5;
		TECH I 5 
		{
			A_AlertMonsters();
			A_Scream();
		}
		#### J 4 A_NoBlocking();
		#### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
	
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,25,0,1);
			A_Recoil(10);
		}
		Goto Death;
		
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		#### N 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		#### OPQRSTU 5;
        #### V -1;
        Stop;
	
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise: //Make her transform into a nirik
		TNT1 A 0 A_GiveInventory("DeadCheck",1);   //gives a token that says "he was revived already"
		FIRE A 2 Bright A_StartFire();
		FIRE BAB 2 Bright;
		FIRE C 2 Bright A_FireCrackle();
		FIRE BCBCDCDCDEDED 2 Bright;
		FIRE E 2 Bright A_FireCrackle();
		FIRE FEFEFGHGHGH 2 Bright;
		TNT1 A 0 A_SpawnItemEx("DemonNirik",0,0,0,0,0,0,0,32);  //Spawns Nirik
		TNT1 A 0 A_Die();
		Stop;
	
	VoidDead:   //...jumps here where the monster is removed
		TNT1 A 1;
		Stop;
	}
}

Class DemonNirik : Actor
{
	Default
	{
	//$Category Monsters
		Tag "$DEMONIRIK";
		
		Monster;
		
		Health 50;
		
		Radius 20; Height 56;
		
		Mass 100;
		
		Speed 10; FastSpeed 15;
		
		PainChance 100;

		+CastSpriteShadow
		+FloorClip
		+ExtremeDeath
		+JumpDown
		+Bright
		 
		SeeSound "demon/sight";
		AttackSound "demon/melee";
		PainSound "demon/pain";
		DeathSound "female/death";
		ActiveSound "demon/active";
		
		Obituary "$NIRIKILL";
	}
	States
	{
	
	Spawn:
		TECH AB 10 A_Look();
		Loop;
	See:
		TECH AAAABBBBCCCCDDDD 1 A_Chase();
		TNT1 A 0 A_JumpIf(Random(1, 10) == 5, "TryJump");
		TNT1 A 0 A_JumpIfCloser(128, "TryJump");
		Loop;
	
	Missile:
	TryJump:
		TNT1 A 0 A_CheckFloor("Jump"); //don't jump if mid-air
		goto See;
	
	Jump:
		TECH H 0 A_StartSound("demon/melee");
		TECH H 0 A_FaceTarget();
		TECH J 8;
		TECH H 0 A_Recoil(-15);
		TECH H 0 A_ChangeVelocity(0, 0, 10, 0);
		Goto See;
	
	Melee:
		TECH E 5 A_FaceTarget();
		TECH F 5 Bright A_CustomMeleeAttack(random(1, 10) * 6);
		TECH F 0 A_Recoil(2);
		
		TECH E 5 A_FaceTarget();
		TECH F 5 Bright A_CustomMeleeAttack(random(1, 10) * 6);
		TECH F 0 A_Recoil(2);
		Goto See;
	
	Pain:
		TECH G 3;
		TECH G 3 A_Pain();
		Goto See;
	
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		TECH H 5;
		TECH I 5 
		{
			A_AlertMonsters();
			A_Scream();
			A_SpawnItem("NirikFire");
		}
		#### J 4 A_NoBlocking();
		#### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
	
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,25,0,1);
			A_Recoil(10);
		}
		Goto Death;
		
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		TECH N 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		#### OPQRSTU 5;
        #### V -1;
        Stop;
		
	Raise:
		FIRE A 2 A_StartFire();
		FIRE BAB 2;
		FIRE C 2 A_FireCrackle();
		FIRE BCBCDCDCDEDED 2;
		FIRE E 2 A_FireCrackle();
		FIRE FEFEFGHGHGH 2;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
	}
}

Class NirikFire : Actor
{
	Default
	{
		+NoInteraction
		+Bright
	}
	States
	{
	Spawn:
		FIRE A 2 A_StartFire();
		FIRE BAB 2;
		FIRE C 2 A_FireCrackle();
		FIRE BCBCDCDCDEDED 2;
		FIRE E 2 A_FireCrackle();
		FIRE FEFEFGHGHGH 2;
		Stop;
	}
}