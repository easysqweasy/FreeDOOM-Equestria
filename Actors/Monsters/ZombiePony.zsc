Class EquestriaZombieMan : DoomEquestriaMonster Replaces ZombieMan
{
	Default
	{
		Monster;
		
		Health 20;
		
		Radius 20; Height 56;
		
		Speed 8;
		
		PainChance 200; PainThreshold 2;
		
		+FLOORCLIP
		+CASTSPRITESHADOW
		
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		
		Obituary "$OB_ZOMBIE";
		
		Tag "$FN_ZOMBIE";

		DropItem "EquestriaClip";
    }
	States
    {
	Spawn:
        POSS A 10 A_Look();
        Loop;

	See:
		#### # 0 A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
        POSS AABBCCDD 4 A_Chase();
        Loop;
	See.Trainer:
		#### AABBCCDD 4 Fast
		{
			A_Chase("Melee", "Missile", CHF_FASTCHASE);
			A_SpawnProjectile("DEWaterSplashChunk", random(32, 56), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		}
		Goto See;
   
	Missile:
        POSS E 10 A_FaceTarget();
        POSS F 8 A_CustomBulletAttack (20, 10, 1, random(1,5)*2, "EquestriaBulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
        POSS E 8;
        Goto See;
    
	Pain:
        POSS G 3;
        POSS G 3 A_Pain();
        Goto See;
   
	Death.Plasma:
		"####" "#" 0 A_SetTranslation("Toasted");
	//Death.BFGSplash:
		"####" "#####" 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
        POSS H 5;
        POSS I 5 A_Scream();
        POSS J 4 A_NoBlocking();
        #### J 1 A_CheckFloor(1);
		Wait;
		#### K 5;
		#### L -1;
		Stop;
    
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(5);
		}
		Goto Death;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,25,0,1);
			A_Recoil(10);
		}
		Goto Death;
		
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		POSS M 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		POSS NOPQRST 5;
        POSS U -1;
        Stop;
	
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
        POSS LKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
        Goto See;
    }
}
