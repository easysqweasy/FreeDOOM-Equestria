Class EquestriaZombieMan : Actor Replaces ZombieMan
{
	Default
	{
		Monster;
		
		Health 20;
		
		Radius 20;
		Height 56;
		
		Speed 8;
		
		PainChance 200;
		
		+FLOORCLIP;
		+CASTSPRITESHADOW;
		
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		
		Obituary "$OB_ZOMBIE";
		
		Tag "$FN_ZOMBIE";

		DropItem "EquestriaClip";
    }
	States
    {
	Spawn:
        POSS A 10 A_Look();
        Loop;

	See:
        POSS AABBCCDD 4 A_Chase();
        Loop;
   
	Missile:
        POSS E 10 A_FaceTarget();
        POSS F 8 A_PosAttack();
        POSS E 8;
        Goto See;
    
	Pain:
        POSS G 3;
        POSS G 3 A_Pain();
        Goto See;
   
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
        POSS H 5;
        POSS I 5 A_Scream();
        POSS J 5 A_NoBlocking();
        POSS K 5;
        POSS L -1;
        Stop;
    
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		POSS M 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,16,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		POSS NOPQRST 5;
        POSS U -1;
        Stop;
	
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Death.Plasma:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		POSS H 5;
        POSS I 5;
        POSS J 5 A_NoBlocking();
        POSS K 5;
        POSS L -1;
        Stop;
    
	Raise:
        POSS LKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
        Goto See;
    }
}
