Class PossessedTrainer : DoomEquestriaMonster
{
	Default
	{
		Monster;
		//$Category Monsters
		//$Title Possessed Trainer
		
		Tag "$BURSTBAT";
		Health 75;
		
		Radius 20; Height 56;
		
		Mass 80;
		Speed 10; FloatSpeed 8;
		
		PainChance 128; PainThreshold 10;
		
		ReactionTime 0;
		
		+FLOATBOB
		+FLOAT
		+NOGRAVITY
		+QUICKTORETALIATE
		+MISSILEMORE
		+AVOIDMELEE
		+BOSS
		
		SeeSound "female/sight";
		AttackSound "weapons/chngun";
		PainSound "female/pain2";
		DeathSound "female/death";
		ActiveSound "female/active";
		
		Obituary "$OB_BURSTBAT";
		
		DropItem "LargeClip";
	}
	States
	{
	Spawn:
		CPOS AB 10 A_Look;
		Loop;

	See:
		CPOS A 0 A_AlertMonsters();
		CPOS AABBCCDD 3 A_Chase("Melee", "Missile", CHF_FASTCHASE);
		Loop;
	
	Kick:
		SKUL C 10 Bright A_FaceTarget();
		SKUL D 4 Bright A_SkullAttack();
		SKUL CD 4 Bright;
		Goto Missile+2;
	
	Melee:
	Dodge:
		CPOS E 0 A_FaceTarget();
		CPOS E 8 ThrustThingZ(0, 50, 0, 0);
		CPOS E 0 A_Stop();
		Goto Missile+2;

	Missile:
		CPOS A 0 A_JumpIfCloser(128, "Dodge");
		//CPOS A 0 A_Jump(128, "Kick");
		
		CPOS E 3 A_FaceTarget();
		CPOS F 4 Bright A_CustomBulletAttack(10, 0, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM);
		CPOS E 3 A_FaceTarget();
		CPOS F 4 Bright A_CustomBulletAttack(15, 10, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM);
		CPOS E 5 A_FaceTarget();
		CPOS F 4 Bright A_CustomBulletAttack(10, 5, 1, random(1,5)*3, "EquestriaBulletPuff", 0, CBAF_NORANDOM);
		Goto See;
	
	Pain:
		CPOS G 3;
		CPOS G 3 A_Pain();
		Goto See;
	
	Death.Plasma:
		"####" "#" 0 A_SetTranslation("Toasted");
		"####" "#####" 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-20, 20), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); bFLOATBOB = 0; }
		
		HEAD G 8;
		HEAD H 8 A_Scream();
		HEAD I 8;
		HEAD J 8;
		HEAD K 8 A_NoBlocking();
		HEAD L -1 A_SetFloorClip();
		Stop;
		
	Death.Shotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		#### H 5;
        #### I 5 A_Scream();
        #### J 5 A_NoBlocking();
        #### K 5;
		#### K 1 A_CheckFloor(1);
		Wait;
		#### L -1;
		Stop;

	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(75, "XDeath");
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		#### H 5;
        #### I 5 A_Scream();
        #### J 5 A_NoBlocking();
        #### K 5;
		#### K 1 A_CheckFloor(1);
		Wait;
		#### L -1;
		Stop;
									
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); bFLOATBOB = 0; }
		
		CPOS O 5
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		CPOS P 5 A_XScream();
		CPOS Q 5;
		CPOS R 5;
		COIS S 5 A_NoBlocking();
		CPOS T -1 A_SetFloorClip();
		Stop;
	
	Death.Berserk:
	XDeath.Berserk:
		"####" "#" 0 ThrustThingZ(0,30,0,1);
		
		"####" "#" 0 A_Jump(256, "MidAir");
		Wait;
	
	MidAir:
		POSS J 1 A_SetRoll((roll + 20));
		Loop;
		
	Crash.Berserk:
		"####" "#" 0 A_SetRoll(0);
		
		"####" "#" 0 A_Jump(256, "XDeath");
		Wait;
		
	Raise:
		CPOS N 5 A_UnSetFloorClip();
		CPOS MLKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; bFLOATBOB = 1; }
		Goto See;
	}
}
