Class EquestriaChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
	Default
	{
		Health 70;
		Radius 20; Height 56;
		Mass 100;
		Speed 8;
		PainChance 170; PainThreshold 5;
		
		+CastSpriteShadow
		
		//SeeSound "chainguy/sight";
		//DeathSound "chainguy/death";
		//PainSound "chainguy/pain";
		AttackSound "weapons/chaingun";
		//ActiveSound "chaingun/active";
		Dropitem "EquestriaChaingun";
		Species "Couple";
	}
	States
	{
	Spawn:
		CPOS AB 10 A_Look();
		Loop;
	
	See:
		#### # 0 A_CheckProximity("See.Trainer", "PossessedTrainer", 1024, 1);
		CPOS AABBCCDD 3 A_Chase();
		Loop;
	See.Trainer:
		#### AABBCCDD 2
		{
			A_Chase("Melee", "Missile");
			A_SpawnProjectile("DEWaterSplashChunk", random(32, 56), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		}
		Goto See;
		
	Missile:
		CPOS E 10 A_FaceTarget();
		CPOS F 3 bright A_CustomBulletAttack(20, 10, 1, random(1,5)*2, "BulletPuff", 0, CBAF_NORANDOM, AAPTR_TARGET, "BulletTracer");
		CPOS E 1 A_CPosRefire();
		Goto Missile+1;
	
	Pain:
		CPOS G 2;
		CPOS G 2 A_Pain();
		Goto See;
	
	Death:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		CPOS H 5;
		CPOS I 5 A_Scream(); 
		TNT1 AAA 0
		{
			A_SpawnProjectile("TinyGibGore1", random(0, 8), 0, random (0, 360), CMF_BADPITCH | CMF_ABSOLUTEPITCH, random (10, 90));	
			A_SpawnProjectile("TinyGibGore2", random(0, 8), 0, random (0, 360), CMF_BADPITCH | CMF_ABSOLUTEPITCH, random (10, 90));
		}
		CPOS J 5 A_NoBlocking();
		CPOS KLM 5;
		CPOS N -1;
		Stop;
		
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(128, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,15,0,1);
			A_Recoil(2.5);
		}
		#### H 5;
        #### I 5 A_Scream();
		#### AAA 0
		{
			A_SpawnProjectile("TinyGibGore1", random(0, 8), 0, random (0, 360), CMF_BADPITCH | CMF_ABSOLUTEPITCH, random (10, 90));	
			A_SpawnProjectile("TinyGibGore2", random(0, 8), 0, random (0, 360), CMF_BADPITCH | CMF_ABSOLUTEPITCH, random (10, 90));
		}
        #### J 5 A_NoBlocking();
        #### KLM 5;
		#### M 1 A_CheckFloor(1);
		Wait;
		#### N -1;
		Stop;
	
	XDeath:
		"####" "#" 0 { bSPRITEFLIP = randompick(0, 1); }
		
		CPOS T 0
		{
			A_SpawnItemEx("XDeathGibber",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_SpawnItemEx("BloodExplosionSphere",0,0,0,0,0,0,0, SXF_TRANSFERPOINTERS);
			A_XScream();
			A_NoBlocking();
		}
		CPOS T -1;
		Stop;
	
	Raise:
		CPOS NMLKJIH 5;
		
		"####" "#" 0 { bSPRITEFLIP = 0; }
		Goto See;
  }
}
