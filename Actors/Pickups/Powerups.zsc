Class EquestriaBackpack : Backpack replaces Backpack
{	Default {	Inventory.PickupSound "pickup/backpack";	}}

Class EquestriaComputerMap : Allmap replaces Allmap
{
	States
	{
	Spawn:
		PMAP A random( 10, 20) Bright;
				PMAP E random( 1, 5) Bright;
		PMAP B random( 10, 20) Bright;
				PMAP E random( 1, 5) Bright;
		PMAP C random( 10, 20) Bright;
				PMAP E random( 1, 5) Bright;
		PMAP D random( 10, 20) Bright;
				PMAP E random( 1, 5) Bright;
		Loop;
	}
}

//Invisibility Sphere

Class EquestriaBlurSphere : CustomInventory replaces BlurSphere
{
	Default
	{
		Height 40; Radius 20;

		+COUNTITEM
		+VISIBILITYPULSE
		+FLOATBOB
		+NOGRAVITY
		+FORCEXYBILLBOARD
		
		+INVENTORY.BIGPOWERUP
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.NOSCREENFLASH
		
		Inventory.PickupMessage "$GOTINVIS"; // "Partial Invisibility"
		Inventory.PickupSound "misc/p_pkup";
		Renderstyle "Translucent";
		Alpha 1;
	}
	States
	{
	Spawn:
		PINS ABCDCBA 6 Bright;
		Loop;
		
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("BlurSphereInventory");
			A_SetBlend("Red", 0.5, 16, "Blue");
		}
		TNT1 A 0 ACS_NamedExecute("BlurMugshot");
		Stop;
	}
}

Class BlurSphereInventory : BlurSphere
{	Default {	RenderStyle "Shaded";	}}

//Invulnerability Sphere

Class EquestriaInvulSphere : CustomInventory replaces InvulnerabilitySphere
{
	Default
	{
		Height 40; Radius 20;
		
		+COUNTITEM
		+FLOATBOB
		+NOGRAVITY
		+FORCEXYBILLBOARD
		
		+INVENTORY.BIGPOWERUP
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.NOSCREENFLASH
		
		Inventory.PickupMessage "$GOTINVUL";
		Inventory.PickupSound "misc/p_pkup";
	}
	States
	{
	Spawn:
		PINV ABCDCBA 6 Bright;
		Loop;
	Pickup:
		TNT1 A 0 A_Quake(4, 2, 0, 100);
		TNT1 A 0 A_GiveInventory("InvulSphereInventory");
		TNT1 A 0 A_SetBlend("DarkGreen", 0.5, 16, "Yellow");
		Stop;
	}
}

Class InvulSphereInventory : InvulnerabilitySphere
{	Default	{	Powerup.Color "GoldMap", 1;	}}

//SoulSphere
Class EquestriaSoulSphere : CustomInventory replaces Soulsphere
{
	Default
	{
		Height 40; Radius 20;
		
		+COUNTITEM
		+FLOATBOB
		+NOGRAVITY
		+FORCEXYBILLBOARD
		
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.NOSCREENFLASH
		
		Inventory.PickupMessage "$GOTSUPER";
		Inventory.PickupSound "misc/p_pkup";
	}
	States
	{
	Spawn:
		SOUL ABCDCBA 6 Bright;
		Loop;
	Pickup:
		TNT1 A 0
		{
			A_SpawnItem("SoulSpherePickup", 0, 10);
			A_GiveInventory("SoulSphereHealth");
			ACS_NamedExecute("Screen_PickSoul");
		}
		Stop;
	}
}

Class SoulSphereHealth : Health	
{
	Default
	{
		Inventory.Amount 100;
		Inventory.MaxAmount 200;
		+INVENTORY.ALWAYSPICKUP
	}
}

//MegaSphere

Class EquestriaMegaSphere : CustomInventory replaces Megasphere
{
	Default
	{
		Height 40; Radius 20;
		
		+COUNTITEM
		+FLOATBOB
		+NOGRAVITY
		+FORCEXYBILLBOARD
		
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.NOSCREENFLASH
		
		Inventory.PickupMessage "$GOTMSPHERE";
		Inventory.PickupSound "misc/p_pkup";
	}
	States
	{
	Spawn:
		MEGA ABCDCBA 6 Bright;
		Loop;
	Pickup:
		TNT1 A 0 A_GiveInventory("BlueArmorForMegasphere", 1);
		TNT1 A 0 A_GiveInventory("MegasphereHealth", 1);
		TNT1 A 0 A_SetBlend("Beige", 0.5, 16);
		Stop;
	}
}

//Berserk

Class EquestriaBerserk : Berserk replaces Berserk
{
	Default
	{
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.NOSCREENFLASH
		Inventory.PickupMessage "$GOTBERSERK";
		Inventory.PickupSound "*xdeath";
	}
	States
	{
	Spawn:
		PSTR A -1;
		Stop;
	Pickup:
		TNT1 A 0
		{
			A_SetMugshotState("Ouch");
			
			HealThing(100, 0);
			
			A_GiveInventory("BerserkEffect");
			A_GiveInventory("BerserkFist");
			A_SelectWeapon("BerserkFist");
			
			A_StartSound("misc/p_pkup", CHAN_VOICE);
			
			A_QuakeEX(4, 4, 4, 20, 0, 100, "", QF_RELATIVE | QF_SCALEDOWN | QF_SCALEUP);
			
			A_SetBlend("Red", 0.5, 160, "Orange");
		}
		Stop;
	}
}

Class BerserkEffect : PowerupGiver
{
	Default
	{
		Powerup.Duration 1;
		Inventory.MaxAmount 0;
		Powerup.Color "None";
		Powerup.Type "PowerStrength";
		
		+INVENTORY.HUBPOWER;
		+INVENTORY.ALWAYSPICKUP;
		+INVENTORY.AUTOACTIVATE;
	}
}

//Unholy Bible

Class DemonicBible : PowerMorph replaces UnholyBible //Madness Shock, Demon Rune, Baroness of Hell
{
	Default
	{
	//$Category "Decoration/Power Ups"
		+ForceXYBillboard;
	}
	States
	{
	Spawn:
		BON4 A -1;
		Stop;
	}
}