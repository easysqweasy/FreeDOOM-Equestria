Class EquestriaChaingun : DoomEquestriaWeapon replaces Chaingun
{
	Default
	{
		//$Title Chaingun
		Weapon.SelectionOrder 4;
		
		Weapon.BobStyle "InverseSmooth"; Weapon.BobRangeX 0.25; Weapon.BobSpeed 1;
		
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		
		Inventory.PickupMessage "$GOTCHAINGUN";
		Inventory.PickupSound "pickup/chaingun";
		
		Obituary "$OB_MPCHAINGUN";
		
		Tag "$TAG_CHAINGUN";
		
		AttackSound "weapons/chaingun";
	}
	States
	{
	Spawn:
		MGUN A -1;
		Stop;
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		CHGG A 1 A_WeaponReady();
		Loop;
	
	Select:
		CHGG A 0 A_SetCrossHair(11);
		
		CHGG A 1 A_Raise();
		Wait;
		
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		CHGG A 1 A_Lower();
		Wait;
	
	Fire:
		CHGG AB 9;
		CHGG AB 6;
		CHGG AB 3;
		CHGG A 1 A_Refire;
		Goto WindDown;
		
	Hold:
		CHGG A 3
		{
			A_FireBullets(6, 6, -1, 4, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash("Flash");
			A_FireProjectile("BulletCasing",90,0,0,random(-8, -4), FPF_NOAUTOAIM);
		}
		
		CHGG B 3
		{
			A_FireBullets(6, 6, -1, 4, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash("Flash2");
			A_FireProjectile("BulletCasing",90,0,0,random(-8, -4), FPF_NOAUTOAIM);
		}

		CHGG A 0 A_ReFire();
		
		CHGG B 0 A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		Goto WindDown;
	
	WindDown:
		CHGG AB 3 A_Refire;
		CHGG AB 6 A_Refire;
		CHGG AB 9 A_Refire;
		CHGG B 1 A_ClearRefire;
		Goto Ready;
		
	Flash:
		CHGF A 2 Bright A_WeaponOffset(0, 28, WOF_INTERPOLATE);
		Goto LightDone;
	
	Flash2:
		CHGF B 2 Bright A_WeaponOffset(0, 36, WOF_INTERPOLATE);
		Goto LightDone;
	
	}
}

Class ChaingunLight : ShotgunLight
{}