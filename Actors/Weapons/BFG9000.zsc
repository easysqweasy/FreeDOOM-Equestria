Class EquestriaBFG9K : DoomEquestriaWeapon replaces BFG9000
{
	Default
	{
		//$Title Big Fucking Gun 9000
		Height 40;
		
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 0.75;
		
		Weapon.SelectionOrder 9;
		Weapon.AmmoUse 50;
		Weapon.AmmoGive 50;
		Weapon.AmmoType "Cell";
		
		+WEAPON.NOAUTOFIRE;
		
		Inventory.PickupMessage "$GOTBFG9000";
		
		Inventory.PickupSound "pickup/bfg";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		BFUG A -1;
		Stop;
	
	Ready:
		BFGG A 1 A_WeaponReady;
		Loop;
	
	Select:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
	
		BFGG A 0 A_SetCrossHair(14);
		BFGG A 1 A_Raise;
		Wait;

	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		BFGG A 1 A_Lower;
		Wait;

	Fire:
		BFGG B 10 A_StartSound("weapons/bfgf", CHAN_WEAPON);
		BFGG B 10 A_Overlay(2, "Shake");
		BFGG B 10
		{
			A_Overlay(-10, "Flash");
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
		}
		BFGG C 4
		{
			A_FireProjectile("EquestriaBFGBall", 0, 1, 0, -8);
			A_StartSound("weapons/bfgshot");
			A_WeaponOffset(0, 45, WOF_KEEPX | WOF_INTERPOLATE);
			A_Overlay(3, "CamRecoil");
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG C 6;
		BFGG B 20
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
	
	Flash:
		BFGF A 0 A_OverlayOffset(-10, 0, 12, WOF_KEEPX);
		
		BFGF B 6 Bright A_Light(4);
		BFGF A 5 Bright A_Light(8);
		BFGF D 8 Bright A_Light(10);
		BFGF A 4 Bright A_Light(6);
		BFGF B 5 Bright;
		Goto LightDone;
	
	Shake:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==0, 2);
		TNT1 A 0 A_QuakeEx(6, 3, 6, 20, 0, 1, "", QF_SCALEUP | QF_RELATIVE);
		
		TNT1 AAAA 1 A_WeaponOffset(frandom(-1.5, 1.5), frandom(28, 32), WOF_INTERPOLATE);
		TNT1 AAAAA 1 A_WeaponOffset(frandom(-3, 3), frandom(32, 36), WOF_INTERPOLATE);
		TNT1 AAAAAAA 1 A_WeaponOffset(frandom(-6, 6), frandom(32, 36), WOF_INTERPOLATE);
		Stop;

	CamRecoil:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==0, "Off");
		TNT1 A 0 A_ZoomFactor(0.85, ZOOM_INSTANT | ZOOM_NOSCALETURNING);

		TNT1 A 2 A_SetPitch(pitch-6, SPF_INTERPOLATE);
		TNT1 B 1 A_ZoomFactor(0.9, ZOOM_INSTANT | ZOOM_NOSCALETURNING);
		TNT1 B 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch+2, SPF_INTERPOLATE);
		TNT1 AAAA 1 A_SetPitch(pitch+0.50, SPF_INTERPOLATE);
		TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		Stop;
	}
}

Class EquestriaBFGBall : Actor replaces BFGBall
{
	Default
	{
		Projectile;
		
		Height 36; Radius 18;
		
		Speed 25;
		
		Damage 50;
		Decal "BFGLightning";
		
		RenderStyle "Add";
		
		+ForceXYBillBoard
		+RollSprite +RollCenter
		+Bright
		+ZDoomTrans
		
		DeathSound "weapons/bfgx";
		Obituary "$OB_MPBFG_BOOM";
	}
	States
	{
	Spawn:
		BFS1 A 0 NoDelay A_SetRoll(random(0, 359));
		BFS1 AAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		BFS1 AAAAA 1 A_SetRoll(roll + random(-75, 75), SPF_INTERPOLATE);
	Projectile:
		BFS1 A 1
		{
			A_SetRoll(roll + random(-45, 45), SPF_INTERPOLATE);
			A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
			A_SpawnItem("BFGTrail",0,0,0);
		}
		Loop;
	
	Death:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		
		BFE1 A 2
		{
			A_Explode(100, 256, 0);
			A_SetRoll(0);
			A_SetScale(2);
			A_SpawnItem("BFGShockwave");
						
			A_QuakeEx(8, 8, 8, 35, 0, 1000, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
		}
		BFE1 BCD 2;
		BFE1 E 8 A_BFGSpray();
		BFE1 FGHIJ 3;
		BFE1 K 1 A_FadeOut(0.1);
		Wait;
	}
}

Class EquestriaBFGExtra : BFGExtra replaces BFGExtra
{	Default { +ForceXYBillBoard; }
	States
	{
	Spawn:
		BFE2 AAAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		BFE2 ABCD 8 Bright;
		Stop;
	}
}

Class BFGTrail : Trails
{
	Default	{	Alpha 0.5;	}
	States
	{
	Spawn:
		TNT1 A 2;
		BFS1 A 0 A_SetRoll(random(0, 359));
		
	Trail:
		BFS1 A random(2,4) Bright A_FadeOut(0.1);
		Loop;
	}
}

Class BFGShockwave : Actor
{
	Default
	{
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+NOGRAVITY
		+NOCLIP
		
		RenderStyle "Add"; Alpha 0.5;
	}
	States
	{
	Spawn:
		BFEF ABCDEGIKMOQSU 1 Bright;
		BFEF W 1 Bright A_FadeOut (0.1);
		Wait;
	}
}

Class BFGSparkles : PuffSparkles
{
	Default
	{
		+NOGRAVITY
		+NOINTERACTION
		Scale 1;
		Speed 15;
	}
	States
	{
	Spawn:
		TNT1 AA 0 A_SetRoll(random(0, 359));
		
		APLS A random(35,45);
		APLS AAAA 1 A_SetScale(Scale.X + 0.25, Scale.Y + 0.25);
		APLS AAAAAAAA 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.90);
			A_Recoil(frandom(-5, 5));
		}	
	}
}