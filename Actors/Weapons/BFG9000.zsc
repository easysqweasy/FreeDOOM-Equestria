Class EquestriaBFG9K : DoomEquestriaWeapon replaces BFG9000
{
	Default
	{
		//$Title Big Fucking Gun 9000
		Height 40;
		
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 0.75;
		
		Weapon.SelectionOrder 2800;
		Weapon.AmmoUse 40;
		Weapon.AmmoGive 40;
		Weapon.AmmoType "Cell";
		
		+WEAPON.NOAUTOFIRE;
		
		Inventory.PickupMessage "$GOTBFG9000";
		
		Inventory.PickupSound "pickup/bfg";
	}
	States
	{
	Spawn:
		BFUG A -1;
		Stop;
	
	Ready:
		BFGG A 1 A_WeaponReady;
		Loop;
	
	Select:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
	
		BFGG A 0 A_SetCrossHair(14);
		BFGG A 1 A_Raise;
		Wait;

	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		BFGG A 1 A_Lower;
		Wait;

	Fire:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==1,"Fire.CamRecoil");
		
		BFGG A 10 A_StartSound("weapons/bfgf", CHAN_WEAPON);
		BFGG A 10 A_Overlay(2, "Shake");
		BFGG B 10
		{
			A_Overlay(-10, "Flash");
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
		}
		BFGG C 4
		{
			A_FireProjectile("EquestriaBFGBall");
			A_WeaponOffset(0, 45, WOF_KEEPX | WOF_INTERPOLATE);
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG C 6;
		BFGG B 20
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
		
	Fire.CamRecoil:	
		BFGG A 10 A_StartSound("weapons/bfgf", CHAN_WEAPON);
		BFGG A 10
		{
			A_QuakeEx(6, 3, 6, 20, 0, 1, "", QF_SCALEUP | QF_RELATIVE);
			A_Overlay(2, "Shake");
		}
		BFGG B 10
		{
			A_Overlay(-10, "Flash");
			A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
		}
		BFGG C 4
		{
			A_FireProjectile("EquestriaBFGBall");
			A_WeaponOffset(0, 45, WOF_KEEPX | WOF_INTERPOLATE);
			A_ZoomFactor(0.85, ZOOM_INSTANT | ZOOM_NOSCALETURNING);
			A_Overlay(2, "CamRecoil");
			//A_SetBlend("green", 0.5, 16);
		}
		BFGG C 6;
		BFGG B 20
		{
			A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
			A_ReFire();
		}
		Goto Ready;
	
	Flash:
		BFGF A 0 A_OverlayOffset(-10, 0, 12, WOF_KEEPX);
		
		BFGF B 6 Bright A_Light(4);
		BFGF A 5 Bright A_Light(8);
		BFGF D 8 Bright A_Light(10);
		BFGF A 4 Bright A_Light(6);
		BFGF B 5 Bright;
		Goto LightDone;
	
	Shake:
		TNT1 AAAA 1 A_WeaponOffset(frandom(-1.5, 1.5), frandom(28, 32), WOF_INTERPOLATE);
		TNT1 AAAAA 1 A_WeaponOffset(frandom(-3, 3), frandom(32, 36), WOF_INTERPOLATE);
		TNT1 AAAAAAA 1 A_WeaponOffset(frandom(-6, 6), frandom(32, 36), WOF_INTERPOLATE);
		Stop;
		
	CamRecoil:
		TNT1 A 2 A_SetPitch(pitch-6, SPF_INTERPOLATE);
		TNT1 B 1 A_ZoomFactor(0.9, ZOOM_INSTANT | ZOOM_NOSCALETURNING);
		TNT1 B 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch+2, SPF_INTERPOLATE);
		TNT1 AAAA 1 A_SetPitch(pitch+0.50, SPF_INTERPOLATE);
		TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		Stop;
	}
}

Class EquestriaBFGBall : BFGBall replaces BFGBall
{
	Default
	{
		Height 36; Radius 18;
		
		Decal "BFGScorch";
		
		RenderStyle "Add";
		Alpha 1;
		
		+FORCEXYBILLBOARD;
	  
		+ROLLSPRITE; +ROLLCENTER;
	}
	States
	{
	Spawn:
		BFS1 AA 0 A_SetRoll(random(-359, 359));
		BFS1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
	Projectile:
		BFS1 AB random(2, 4) Bright A_SpawnItemEx("BFGTrail");
		BFS1 AAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		Loop;
	
	Death:
		TNT1 A 0;
		
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		
		BFE1 A 2 Bright
		{
			A_SetRoll(0);
			A_SetScale(1.50);
			A_SpawnItem("BFGShockwave");
			
			A_SpawnItemEx("BFGFloorCheck", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
			
			A_QuakeEx(8, 8, 8, 35, 0, 1000, "world/quake", QF_SCALEDOWN, 0,0,0, 1);
		}
		BFE1 BCD 2 Bright;
		BFE1 E 8 Bright A_BFGSpray;
		BFE1 FGHIJ 3 Bright;
		BFE1 K 1 Bright A_FadeOut(0.1);
		Wait;
	}
}

Class EquestriaBFGExtra : BFGExtra replaces BFGExtra
{	Default { +ForceXYBillBoard; }	}

Class BFGTrail : Trails
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_SetRoll(random(-359, 359));
		
	Trail:
		BFS1 AB random(2,4) Bright A_FadeOut(0.1);
		TNT1 AA random(2,4) A_FadeOut(0.1);
		Loop;
   }
}

Class BFGShockwave : Actor
{
	Default
	{
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+NOGRAVITY
		+NOCLIP
		
		RenderStyle "Add";
		Alpha 0.50;
		
		Scale 0.70;
	}
	States
	{
	Spawn:
		BFEF EGIKMOQSUWY 1 Bright;
		BFEF Z 1 Bright A_FadeOut (0.1);
		Wait;
	}
}

Class BFGSparkles : PuffSparkles
{
	Default
	{
		+NOGRAVITY
		+NOINTERACTION
		Scale 1;
		BounceFactor 0.15;
		Speed 15;
		Gravity 0.10;
	}
	States
	{
	Spawn:
		PLSS AA 0 A_SetRoll(random(-359, 359));
		
		PLSS A 16;
		PLSS AAA 4;
		BFE2 DDDDD 1 A_SetScale(Scale.X - 0.25, Scale.Y - 0.25);
		Stop;
	}
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.90);
			A_Recoil(random(-5, 5));
		}	
	}
}