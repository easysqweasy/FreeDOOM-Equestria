Class EquestriaSuperShotgun : DoomEquestriaWeapon replaces SuperShotgun
{
	Default
	{
		//$Title Super Shotgun
		Weapon.SelectionOrder 2;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		
		Inventory.PickupMessage "$GOTSHOTGUN2";
		Obituary "$OB_MPSSHOTGUN";
		Tag "$TAG_SUPERSHOTGUN";
	}
	States
	{
	Spawn:
		SGN2 A -1;
		Stop;

	Select:
		SHT2 A 0 A_SetCrossHair(10);
		
		SHT2 A 1 A_Raise();
		Wait;
	
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		
		SHT2 A 1 A_Lower();
		Wait;
		
	Deselect2:
		SHT2 C 1 A_Lower();
		Wait;
  
	/*SelectAnimation:
		SHTG A 0 A_StartSoundEX("misc/w_pkup", 1);
		SHT2 DCB 2;*/
	
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		SHT2 A 1 A_WeaponReady();
		Loop;

	Fire:
		SHT2 A 8
		{
			A_FireBullets (11.2, 7.1, 20, 5, "Player_ShotgunPuff", 1, 0, "BulletTracer", -8);
			A_FireProjectile("SuperShotgunLight", 0, 0, 0, -5);
			A_StartSound ("weapons/sshotf", CHAN_WEAPON);
			A_GunFlash();
			A_Overlay(2, "CamRecoil");
			A_Overlay(3, "Offset");
		}
		Goto Reloading;
	
	Reloading:
		SHT2 BCDD 1;
		SHT2 D 1;
		SHT2 E 3;
		SHT2 F 4;
		SHT2 G 5 A_JumpIfNoAmmo("Deselect2");
		SHT2 H 5
		{
			A_OpenShotgun2();
			A_FireProjectile("ShellCasing",90,0,0,-4, FPF_NOAUTOAIM);
			A_FireProjectile("ShellCasing",45,0,0,-4, FPF_NOAUTOAIM);
		}
		SHT2 IJ 4;
		SHT2 K 6 A_LoadShotgun2;
		SHT2 LM 4;
		SHT2 NO 3;
		SHT2 P 2 A_CloseShotgun2;
		SHT2 A 0 A_ReFire;
		Goto Ready;
	
	Flash:
		SHT2 Q 1 Bright;
		SHT2 R 3 Bright;
		SHT2 Q 2 Bright;
		Goto LightDone;
	
	CamRecoil:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==0, "Off");
		
		TNT1 B 1 A_ZoomFactor(0.975, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch-4, SPF_INTERPOLATE);
		TNT1 B 1 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 AAAAA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 AAAAAA 1 A_SetPitch(pitch+0.25, SPF_INTERPOLATE);
		Stop;
		
	Offset:
		TNT1 A 4 A_WeaponOffset(0, 45, WOF_KEEPX | WOF_INTERPOLATE);
		TNT1 A 5 A_WeaponOffset(0, 40, WOF_KEEPX | WOF_INTERPOLATE);
		TNT1 A 3 A_WeaponOffset(0, 35, WOF_KEEPX | WOF_INTERPOLATE);
		TNT1 A 0 A_WeaponOffset(0, 32, WOF_KEEPX | WOF_INTERPOLATE);
		Stop;
		
	Overlay.Aside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHT2 ACC 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 4, WOF_ADD);
		}
		SHT2 DD 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 8, WOF_ADD);
		}
		SHT2 D 10;
		SHT2 DCC 1
		{
			A_OverlayOffset(3, 0, 0, WOF_INTERPOLATE );
			A_OverlayRotate(3, -8, WOF_ADD);
		}
		SHT2 A 1;
		Stop;
	
	Overlay.AirAside:
		TNT1 A 0 A_OverlayFlags(3, PSPF_ADDBOB, true);
		TNT1 A 0 A_OverlayPivotAlign(3, PSPA_RIGHT, PSPA_BOTTOM);
		
		SHT2 ACC 1
		{
			A_OverlayOffset(3, -35, 5, WOF_INTERPOLATE );
			A_OverlayRotate(3, 5, WOF_ADD);
		}
		SHT2 DD 1
		{
			A_OverlayOffset(3, -30, 10, WOF_INTERPOLATE );
			A_OverlayRotate(3, 10, WOF_ADD);
		}
		SHT2 D 8;
		SHT2 DCC 1 
		{
			A_OverlayOffset(3, 0, 0, WOF_KEEPY | WOF_INTERPOLATE );
			A_OverlayRotate(3, -10, WOF_ADD);
		}
		SHT2 A 1;
		Stop;
	}
}

Class SuperShotgunLight : PlasmaGunLight
{}