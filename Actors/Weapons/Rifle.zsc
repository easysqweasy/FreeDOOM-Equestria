Class EquestriaRifle : DoomEquestriaWeapon
{
	Default
	{
		Weapon.SelectionOrder 3;
		Weapon.BobStyle "InverseSmooth";
		
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 30;
		Weapon.AmmoType "Clip";
		
		Inventory.PickupMessage "$GOTRIFLE";
		Obituary "$OB_MPCHAINGUN";
		Tag "Rifle";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bSPRITEFLIP = RandomPick(0,1); }
		RFLE D -1;
		Stop;
		
	Ready:
		TNT1 A 0 A_JumpIfInventory("KickCheck", 1, "Kick.Normal");
		
		RFLE A 1 A_WeaponReady;
		Loop;
	Deselect:
		TNT1 A 0 A_TakeInventory("KickCheck", 999);
		TNT1 A 0 A_JumpIf(health <= 0, "DeathCheck");
		
		RFLE A 1 A_Lower();
		Loop;
	Select:
		RFLE A 1 A_Raise();
		Loop;
	Fire:
		RFLE A 2
		{
			A_FireBullets(2, 2, 1, 5, "Player_BulletPuff", 1, 0, "BulletTracer", -8);
			A_GunFlash();
			A_StartSound("weapons/chngun", CHAN_WEAPON);
		
			A_WeaponOffset(0, 34, WOF_INTERPOLATE | WOF_KEEPX);
			A_Overlay(2, "CamRecoil");
			A_FireProjectile("BulletCasing",-90,0,0,random(-8, -4), FPF_NOAUTOAIM);
		}
		
		RFLE A 2 A_WeaponOffset(0, 36, WOF_INTERPOLATE | WOF_KEEPX);
		RFLE A 0 A_ReFire();
		RFLE A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE | WOF_KEEPX);
		Goto Ready;
	Flash:
		RFLE C 2 Bright;
		RFLE B 2 Bright;
		Goto LightDone;
		
	CamRecoil:
		TNT1 A 0 A_JumpIf(callACS("CamRecoilToggle")==0, "Off");
		
		TNT1 A 0 A_ZoomFactor(0.998, ZOOM_NOSCALETURNING);
		TNT1 AA 1 A_SetPitch(pitch-0.25);
		TNT1 A 0 A_ZoomFactor(1, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_SetPitch(pitch+0.5);
		Stop;
	}
}