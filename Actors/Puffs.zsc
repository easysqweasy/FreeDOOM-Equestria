Class PuffSparkles : DEParticles
{
	Default
	{
		-NOGRAVITY
		+Bright
		+ThruActors
		
		BounceType "Doom";
		
		Scale 0.50;
		
		Height 2; Radius 1;
		
		Gravity 0.25;
		
		Speed 4;
		
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		
		PUFF A 10;
		PUFF AAA 4 A_SetScale(Scale.X - 0.10, Scale.Y - 0.10);
		PUFF AA 3 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF AAAA 2 A_SetScale(Scale.X - 0.025, Scale.Y - 0.025);
		Stop;
		
	Death:
		TNT1 A 0;
		Stop;
		
	Vanish:
		PUFF A 1 A_FadeOut(0.1);
		Wait;
	}
}

Class EquestriaBulletPuff : Actor replaces BulletPuff
{
	Default
	{
		Height 8; Radius 4;
		RenderStyle "Translucent"; Alpha 0.5;
		VSpeed 1;
		Mass 5;
		Scale 0.75;
		
		AttackSound "weapons/ricochet";
		
		+NOGRAVITY +NOBLOCKMAP +NOTELEPORT
		+FORCEXYBILLBOARD
		+ROLLSPRITE +ROLLCENTER
	}
	States
	{
	
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 AAA 0 A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(5, 8) Bright;
		PUFF B 4;
	
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	Vanish:
		TNT1 A 0;
		Stop;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.96);
		}
	}
}

Class Player_BulletPuff : EquestriaBulletPuff
{	Default	{	AttackSound "";	}	}

Class ChainsawSparkles : PuffSparkles
{
	Default
	{
		Gravity 0.5;
		Speed 6;
	}
	States
	{
	Spawn:
		TNT1 A 0 ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		PUFF A 1;
		Loop;
	
	Death:
		PUFF AAA 4 A_SetScale(Scale.X - 0.10, Scale.Y - 0.10);
		PUFF AA 3 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF AAAA 2 A_SetScale(Scale.X - 0.025, Scale.Y - 0.025);
		Stop;
	}
}

Class ChainsawPuff : Player_BulletPuff
{
	Default
	{
		Damagetype "Chainsaw";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 AAAAA 0 A_SpawnProjectile("ChainsawSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(0, 359));
		}
		
		PUFF A random(5, 8) Bright;
		//PUFF A 0 A_Recoil(-2);
		PUFF B 4;
		Goto Melee;
	}
}

Class ShotgunPuff : EquestriaBulletPuff
{
	Default
	{
		Scale 1;	DamageType "Shotgun";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecute("CheckLiquid");
		TNT1 A 0 A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		
		PUFF A random(2, 8) Bright;
		PUFF B 4;
		
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.05);
		Wait;
	}
}

Class Player_ShotgunPuff : ShotgunPuff
{	Default	{	AttackSound "";	}	}

Class Player_SuperShotgunPuff : Player_ShotgunPuff
{	Default	{	DamageType "SuperShotgun";	}	}

Class KickPuff : ShotgunPuff
{
	Default
	{
		DamageType "Kick";
		AttackSound "*fist";
		
		+BLOODLESSIMPACT;
		+NODECAL;
	}
	States
	{
	
	Spawn:
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	}
}

Class BerserkPuff : KickPuff
{
	Default
	{
		DamageType "Berserk";
		Alpha 0.5;
		Scale 0.5;
		
		-BLOODLESSIMPACT;
	}
	States
	{
	
	Spawn:
	Melee:
		RSMK A 0;
		RSMK A 1
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		RSMK AAAAAABBBBBBCCCCCDDDDDEEEEE 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		Wait;
	}
}
