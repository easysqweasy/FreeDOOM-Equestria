Class PuffSparkles : DEParticles
{
	Default
	{
		-NOGRAVITY
		+Bright
		+ThruActors
		
		BounceType "Doom";
		
		Scale 0.50;
		
		Height 2; Radius 1;
		
		Gravity 0.25;
		
		Speed 4;
		
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		
		PUFF A 10;
		PUFF AAA 4 A_SetScale(Scale.X - 0.10, Scale.Y - 0.10);
		PUFF AA 3 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF AAAA 2 A_SetScale(Scale.X - 0.025, Scale.Y - 0.025);
		Stop;
		
	Death:
		TNT1 A 0;
		Stop;
		
	Vanish:
		PUFF A 1 A_FadeOut(0.1);
		Wait;
	}
}

Class EquestriaBulletPuff : BulletPuff replaces BulletPuff
{
	Default
	{
		Height 8; Radius 4;
		Scale 0.75;
		VSpeed 1;
		
		AttackSound "weapons/ricochet";
		
		+NOTELEPORT
		+FORCEXYBILLBOARD
		-RANDOMIZE
		+ROLLSPRITE +ROLLCENTER
	}
	States
	{
	
	Spawn:
		TNT1 AA 0 ACS_NamedExecute("CheckLiquid");
		TNT1 AAA 0 NoDelay A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		TNT1 A 0 A_SpawnProjectile("BulletDecalCheck", 7,0,0, CMF_TRACKOWNER | CMF_AIMDIRECTION, 90);
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		
		PUFF A random(5, 8) Bright;
		PUFF B 4;
	
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	Vanish:
		TNT1 A 0;
		Stop;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.96);
		}
	}
}

Class Player_BulletPuff : EquestriaBulletPuff
{	Default	{	AttackSound "weapons/impact";	}	}

Class ChainsawSparkles : PuffSparkles
{
	Default
	{
		Gravity 1;
		
		Speed 6;
	}
}

Class ChainsawPuff : Player_BulletPuff
{
	Default
	{
		Damagetype "Chainsaw";
	}
	States
	{
	Spawn:
		TNT1 AAAAAAAAAA 0 NoDelay A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		TNT1 A 0 A_SpawnProjectile("BulletDecalCheck", 7,0,0, CMF_TRACKOWNER | CMF_AIMDIRECTION, 90);
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		
		PUFF A random(5, 8) Bright;
		//PUFF A 0 A_Recoil(-2);
		PUFF B 4;
		Goto Melee;
	}
}

Class ShotgunPuff : EquestriaBulletPuff
{
	Default
	{
		Scale 1;
	}
	States
	{
	
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
			A_SpawnProjectile("BulletDecalCheck", 7,0,0, CMF_TRACKOWNER | CMF_AIMDIRECTION, 90);
		}
		TNT1 A 0
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		
		PUFF A random(2, 8) Bright;
		PUFF B 4;
		
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.05);
		Wait;
	}
}

Class Player_ShotgunPuff : ShotgunPuff
{	Default	{	AttackSound "weapons/impact";	}	}

Class KickPuff : ShotgunPuff
{
	Default
	{
		DamageType "Kick";
		AttackSound "*fist";
		
		+BLOODLESSIMPACT;
		+NODECAL;
	}
	States
	{
	
	Spawn:
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	}
}

Class BerserkPuff : KickPuff
{
	Default
	{
		DamageType "Berserk";
		Alpha 0.5;
		Scale 0.5;
		
		-BLOODLESSIMPACT;
	}
	States
	{
	
	Spawn:
	Melee:
		RSMK A 0;
		RSMK A 1
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		RSMK AAAAAABBBBBBCCCCCDDDDDEEEEE 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		Wait;
	}
}
