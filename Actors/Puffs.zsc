Class PuffSparkles : DEParticles
{
	Default
	{
		-NOGRAVITY
		+Bright
		+ThruActors
		
		BounceType "Doom";
		BounceFactor 0.50;
		
		Scale 0.50;
		
		Height 2; Radius 1;
		
		Gravity 0.25;
		
		Speed 4;
		
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Vanish");
		
		PUFF A 5;
		PUFF AAA 4 A_SetScale(Scale.X - 0.10, Scale.Y - 0.10);
		PUFF AA 3 A_SetScale(Scale.X - 0.05, Scale.Y - 0.05);
		PUFF AAAA 2 A_SetScale(Scale.X - 0.025, Scale.Y - 0.025);
		Stop;
		
	Death:
		Stop;
		
	Vanish:
		PUFF A 1 A_FadeOut(0.25);
		Wait;
	}
}

Class EquestriaBulletPuff : BulletPuff replaces BulletPuff
{
	Default
	{
		Height 6; Radius 3;
		Scale 0.75;
		VSpeed 1;
		
		AttackSound "weapons/ricochet";
		
		+FLOORCLIP
		+NOTELEPORT
		+FORCEXYBILLBOARD
		+NOBLOCKMAP
		-RANDOMIZE
		+SHOOTABLE
	}
	States
	{
	
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIfInventory("Vanish", 1, "Vanish");
		TNT1 AAA 0 A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		TNT1 A 0 A_SpawnProjectile("BulletDecalCheck", 7,0,0, CMF_TRACKOWNER | CMF_AIMDIRECTION, 90);
		
		PUFF A random(5, 8) Bright;
		//PUFF A 0 A_Recoil(-2);
		PUFF B 4;
	
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	Vanish:
		Stop;
	}
	
	Override void Tick()
	{
		Super.Tick();
		if (!isFrozen())
		{
			A_ScaleVelocity(0.96);
		}
	}
}

Class ShotgunPuff : EquestriaBulletPuff
{
	Default
	{
		Scale 1;
	}
	States
	{
	
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_JumpIfInventory("Vanish", 1, "Vanish");
		TNT1 A 0 A_SpawnProjectile("PuffSparkles", 0, 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		TNT1 A 0 A_SpawnProjectile("BulletDecalCheck", 7,0,0, CMF_TRACKOWNER | CMF_AIMDIRECTION, 90);

		PUFF A random(2, 8) Bright;
		PUFF B 4;
		
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.05);
		Wait;
	}
}

Class KickPuff : ShotgunPuff
{
	Default
	{
		DamageType "Kick";
		AttackSound "*fist";
		
		+BLOODLESSIMPACT;
		+NODECAL;
	}
	States
	{
	
	Spawn:
	Melee:
		PUFF C 4;
		PUFF D 1 A_FadeOut(0.1);
		Wait;
	
	}
}

Class BerserkPuff : KickPuff
{
	Default
	{
		DamageType "Berserk";
		Alpha 0.5;
		Scale 0.5;
		
		-BLOODLESSIMPACT;
	}
	States
	{
	
	Spawn:
	Melee:
		TNT1 A 0;
		RSMK A random(2,4)
		{
			bSPRITEFLIP = randompick(0, 1);
			A_SetRoll(random(-359, 359));
		}
		RSMK A 2;
		RSMK AAAAAAAAAABBBBBBBBCCCCCCDDDD 1
		{
			A_FadeOut(0.02);
			A_SetScale(Scale.X + 0.025, Scale.Y + 0.025);
		}
		RSMK E 1 A_FadeOut(0.05);
		Wait;
	}
}
