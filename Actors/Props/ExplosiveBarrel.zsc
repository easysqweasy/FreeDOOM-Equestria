Class EquestriaExploBarrel : ExplosiveBarrel replaces ExplosiveBarrel
{
	Default
	{
		Radius 10; Height 32;
		
		DamageType "BarrelExp";
		
		+NOBLOOD;
		+ALLOWPAIN;
		-OLDRADIUSDMG;
		+CANPASS;
		
		//BloodColor "66 a2 66";
		//BloodType "ExploBarrel"
		Painchance 256;
	}
	States
	{
	Pain:
		BAR1 "#" 1 A_SpawnItem("SplashSmoke", 0, 40);
	Spawn:
		BAR1 A 8;
		Loop;
	
	Death.BarrelExp:
	XDeath.BarrelExp:
	Death:
		BAR1 AAAA 1 Bright A_SpawnProjectile("Smoke", random(0, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));

		BAR1 AAAAA 1 Bright A_SetScale(Scale.X + 0.05, 1.0, 1, true);
		BEXP AAAAA 1 Bright A_SetScale(Scale.X + 0.2, Scale.Y + 0.025);
		
		TNT1 AAAAAAAAAA 0 A_SpawnProjectile("Smoke", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		
		TNT1 BBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP B 6 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(0, 32), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP CCCCC 1 A_FadeOut(0.1);
		BEXP D 1 A_FadeOut(0.1);
		Wait;
		
	Death.SuperShotgun:
		#### # 0 A_JumpIfCloser(150, 1);
		Goto Death;
		#### # 0
		{
			A_FaceTarget();
			ThrustThingZ(0,20,0,1);
			A_Recoil(7.5);
		}
		Goto Death;
		
	XDeath:
		TNT1 AAAA 0 A_SpawnProjectile("Smoke", random(0, 32), 0, random (0, 359), CMF_AIMDIRECTION, random (0, -180));
		
		BAR1 AAA 1 Bright A_SetScale(Scale.X + 0.4, Scale.Y + 0.025);
		BEXP A 4 Bright;
		
		TNT1 AAAAAAAAAA 0 A_SpawnProjectile("Smoke", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (0, 359));
		
		TNT1 BBBBBBBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(0, 32), random(-20, 20), random (0, 359), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP B 8 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(0, 32), random (-40, 40), random (0, 359), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP CCCCC 1 A_FadeOut(0.1);
		BEXP D 1 A_FadeOut(0.1);
		Wait;
	}
}

Class EmptyBarrel : EquestriaExploBarrel
{
	Default
	{
		+NOBLOOD
		-ALLOWPAIN
		-OLDRADIUSDMG
	}
	States
	{
	Spawn:
		BAR1 A -1;
		Stop;
		
	Death:
		BEXP ABCDE 4;
		Stop;
	}
}

Class BarrelDebris : DebrisCore
{
	Default
	{
		Radius 10; Height 10;
		Mass 50;
		
		Speed 12;
		
		Gravity 0.5;
		
		+ROLLSPRITE +ROLLCENTER
		DebrisCore.RollSpeed 35;
		
		+BOUNCEONWALLS +BOUNCEONFLOORS +USEBOUNCESTATE
		BounceType "Doom";
		BounceFactor 0.25;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { A_SetRoll(random(0,359)); bSPRITEFLIP = randompick(0,1); }
		Goto ChooseDebris;
	
	Bounce:
		#### # 0 A_SetRoll(roll + random(-100, 100));
	Roll:
		#### # 1 A_SpawnItem("BarrelDebrisSmoke");
		Loop;
		
	Death:
	Crash:
		#### # 350;
		#### # 0 A_JumpIf(GetCVar("GibsStayForever") == 0, "FadeOut");
		
	FadeOut:
		#### # 1 A_FadeOut(0.1);
		Wait;
	
	ChooseDebris:
		TNT1 A 0 A_Jump(256, 1, 2, 3, 4);
		Wait;
		EXPD A 0;
		Goto Roll;
		EXPD B 0;
		Goto Roll;
		EXPD C 0;
		Goto Roll;
		EXPD D 0;
		Goto Roll;
	}
}

Class BarrelDebrisSmoke : EquestriaRocketSmoke
{	Default{	Alpha 0.35;	Scale 0.2;	VSpeed 0;}	}