Class EquestriaExploBarrel : ExplosiveBarrel replaces ExplosiveBarrel
{
	Default
	{
		Radius 10; Height 32;
		
		DamageType "BarrelExp";
		
		+NOBLOOD;
		+ALLOWPAIN;
		-OLDRADIUSDMG;
		+CANPASS;
		
		//BloodColor "66 a2 66";
		//BloodType "ExploBarrel"
		Painchance 256;
	}
	States
	{
	Pain:
		BAR1 "#" 1 A_SpawnItem("SplashSmoke", 0, 40);
	Spawn:
		BAR1 AB 8;
		Loop;
	
	Death.BarrelExp:
	XDeath.BarrelExp:
	Death:
		BEXP AAAA 1 Bright A_SpawnProjectile("RocketSmoke", random(16, 32), 0, random (0, 360), CMF_AIMDIRECTION, random (0, -180));

		BEXP BBBBB 1 Bright A_SetScale((Scale.X + 0.05), 1.0, 0, true);
		BEXP CCCCC 1 Bright A_SetScale((Scale.X + 0.1), 1.0, 0, true);
		
		TNT1 AAAAAAAAAA 0 A_SpawnProjectile("RocketSmoke", random(-32, 32), 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		
		TNT1 BBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(8, 16), 0, random (0, 360), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP D 5 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-40, 40), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP EEEEEEEEEE 1 Bright A_FadeOut(0.1);
		TNT1 A 1050 Bright A_BarrelDestroy();
		TNT1 A 5
		{
			bFORCEXYBILLBOARD = 0;
			A_Respawn();
		}
		Stop;
  
	XDeath:
		BEXP A 1 Bright;
		
		TNT1 AAAA 0 Bright A_SpawnProjectile("RocketSmoke", random(16, 32), 0, random (0, 360), CMF_AIMDIRECTION, random (0, -180));
		
		BEXP BBB 1 Bright A_SetScale((Scale.X + 0.20), 1.0, 0, true);
		BEXP C 4 Bright;
		
		TNT1 AAAAAAAAAA 0 A_SpawnProjectile("RocketSmoke", random(-32, 32), 0, random (0, 360), CMF_AIMDIRECTION, random (0, 360));
		
		TNT1 BBBBBBBBBBBBBBBBBBBB 0 A_SpawnProjectile("BarrelDebris", random(8, 16), 0, random (0, 360), CMF_AIMDIRECTION, random (-10, -90));
		
		BEXP D 5 Bright
		{
			A_Scream();
			A_Explode();
			A_SetScale(1.5, 1.5);
			A_QuakeEx(3, 3, 3, 4, 0, 300, "", 0,0,0,0, 1);
			
			bFORCEXYBILLBOARD = 1;
		}
		
		TNT1 AAAA 0 A_SpawnProjectile("SplashSmoke", random(-20, 20), random (-40, 40), random (0, 360), CMF_AIMDIRECTION, random(0, -90));
		
		BEXP EEEEEEEEEE 1 Bright A_FadeOut(0.1);
		TNT1 A 1050 Bright A_BarrelDestroy();
		TNT1 A 5
		{
			bFORCEXYBILLBOARD = 0;
			A_Respawn();
		}
		Stop;

	}
}

Class EmptyBarrel : EquestriaExploBarrel
{
	Default
	{
		+NOBLOOD
		-ALLOWPAIN
		-OLDRADIUSDMG
	}
	States
	{
	Spawn:
		BAR1 A -1;
		Stop;
		
	Death:
		BEXP ABCDE 4;
		Stop;
	}
}

Class BarrelDebris : DebrisCore
{
	Default
	{
		Radius 10; Height 10;
		Mass 50;
		
		Speed 12;
		
		Gravity 0.75;
		
		+ROLLSPRITE +ROLLCENTER
		
		+BOUNCEONWALLS +BOUNCEONCEILINGS +BOUNCEONFLOORS +BOUNCEONACTORS
		BounceType "Doom";
		BounceFactor 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(frandom(1.0, 2.5));
		TNT1 A 0 A_SetRoll(random(0,360));
		TNT1 A 0 { bSPRITEFLIP = randompick(0,1); }
		Goto ChooseDebris;
	
	Roll:
		"####" # 1 A_SetRoll((roll + 15), SPF_INTERPOLATE);
		Loop;
	
	Death:
		"####" # 350;
		"####" # 1 A_FadeOut(0.1);
		Wait;
	
	ChooseDebris:
		TNT1 A 0 A_Jump(256, 1, 2, 3, 4, 5, 6, 7);
		EXPD A 0;
		Goto Roll;
		EXPD B 0;
		Goto Roll;
		EXPD C 0;
		Goto Roll;
		EXPD D 0;
		Goto Roll;
		EXPD E 0;
		Goto Roll;
		EXPD F 0;
		Goto Roll;
		EXPD G 0;
		Goto Roll;
	}
}